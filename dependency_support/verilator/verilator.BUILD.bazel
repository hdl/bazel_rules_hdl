# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Original implementation by Kevin Kiningham (@kkiningh) in kkiningh/rules_verilator.
# Ported to bazel_rules_hdl by Stephen Tridgell (@stridge-cruxml)

load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("@rules_hdl//dependency_support/com_github_westes_flex:flex.bzl", "genlex")
load(
    "@rules_hdl//dependency_support/verilator/private:verilator_utils.bzl",
    "verilator_astgen",
    "verilator_bisonpre",
    "verilator_flexfix",
)
load("@rules_python//python:defs.bzl", "py_binary")

package(default_visibility = ["//visibility:private"])

licenses(["notice"])

exports_files([
    "Artistic",
    "COPYING",
    "COPYING.LESSER",
])

# `py_binary` does not accept sources that do not end in `.py`. So to
# successfully turn `bisonpre` into a binary, the source needs to be renamed.
copy_file(
    name = "bisonpre_src",
    src = "src/bisonpre",
    out = "src/bisonpre.py",
)

py_binary(
    name = "bisonpre",
    srcs = ["src/bisonpre.py"],
    main = "src/bisonpre.py",
)

verilator_astgen(
    name = "verilator_astgen",
    srcs = [
        "src/V3Ast.h",
        "src/V3AstNodeDType.h",
        "src/V3AstNodeExpr.h",
        "src/V3AstNodeOther.h",
        "src/V3DfgVertices.h",
        "src/Verilator.cpp",
    ],
    outs = [
        "V3Ast__gen_forward_class_decls.h",
        "V3Ast__gen_impl.h",
        "V3Ast__gen_macros.h",
        "V3Ast__gen_report.txt",
        "V3Ast__gen_type_enum.h",
        "V3Ast__gen_type_info.h",
        "V3Ast__gen_type_tests.h",
        "V3Ast__gen_visitor_decls.h",
        "V3Ast__gen_visitor_defns.h",
        "V3Ast__gen_yystype.h",
        "V3Dfg__gen_ast_to_dfg.h",
        "V3Dfg__gen_auto_classes.h",
        "V3Dfg__gen_dfg_to_ast.h",
        "V3Dfg__gen_forward_class_decls.h",
        "V3Dfg__gen_macros.h",
        "V3Dfg__gen_type_enum.h",
        "V3Dfg__gen_type_tests.h",
        "V3Dfg__gen_visitor_decls.h",
        "V3Dfg__gen_visitor_defns.h",
    ],
    args = [
        "--astdef",
        "V3AstNodeDType.h",
        "--astdef",
        "V3AstNodeExpr.h",
        "--astdef",
        "V3AstNodeOther.h",
        "--dfgdef",
        "V3DfgVertices.h",
        "--classes",
    ],
    astgen = "src/astgen",
)

verilator_astgen(
    name = "verilator_astgen_const",
    srcs = [
        "src/V3Ast.h",
        "src/V3AstNodeDType.h",
        "src/V3AstNodeExpr.h",
        "src/V3AstNodeOther.h",
        "src/V3Const.cpp",
        "src/V3DfgVertices.h",
        "src/Verilator.cpp",
    ],
    outs = [
        "V3Const__gen.cpp",
    ],
    args = [
        "--astdef",
        "V3AstNodeDType.h",
        "--astdef",
        "V3AstNodeExpr.h",
        "--astdef",
        "V3AstNodeOther.h",
        "--dfgdef",
        "V3DfgVertices.h",
        "V3Const.cpp",
    ],
    astgen = "src/astgen",
)

genlex(
    name = "verilator_lex_pregen",
    src = "src/verilog.l",
    out = "V3Lexer_pregen.yy.cpp",
)

verilator_flexfix(
    name = "verilator_lex_flexfix",
    src = ":V3Lexer_pregen.yy.cpp",
    out = "V3Lexer.yy.cpp",
    args = ["V3Lexer"],
    flexfix = "src/flexfix",
)

genlex(
    name = "verilator_prelex_pregen",
    src = "src/V3PreLex.l",
    out = "V3PreLex_pregen.yy.cpp",
)

verilator_flexfix(
    name = "verilator_prelex_flexfix",
    src = ":V3PreLex_pregen.yy.cpp",
    out = "V3PreLex.yy.cpp",
    args = ["V3PreLex"],
    flexfix = "src/flexfix",
)

verilator_bisonpre(
    name = "verilator_bison",
    srcs = [
        "src/verilog.y",
        "@org_gnu_bison//:data",
    ],
    outs = [
        "V3ParseBison.c",
        "V3ParseBison.h",
    ],
    args = [
        "--yacc",
        "$(execpath @org_gnu_bison//:bison)",
        "-d",
        "-v",
        "-o",
        "$(execpath V3ParseBison.c)",
        "$(execpath src/verilog.y)",
    ],
    bisonpre = ":bisonpre",
    env = {
        "BISON_PKGDATADIR": "$(execpath @org_gnu_bison//:data)",
        "M4": "$(execpath @org_gnu_m4//:m4)",
    },
    tools = [
        "@org_gnu_bison//:bison",
        "@org_gnu_m4//:m4",
    ],
)

cc_library(
    name = "verilatedos",
    hdrs = ["include/verilatedos.h"],
    strip_include_prefix = "include/",
)

# TODO(kkiningh): Verilator also supports multithreading, should we enable it?
cc_library(
    name = "verilator_libV3",
    srcs = glob(
        ["src/V3*.cpp"],
        exclude = [
            "src/V3*_test.cpp",
            "src/V3Const.cpp",
        ],
    ) + [
        ":V3Ast__gen_forward_class_decls.h",
        ":V3Ast__gen_impl.h",
        ":V3Ast__gen_macros.h",
        ":V3Ast__gen_type_enum.h",
        ":V3Ast__gen_type_info.h",
        ":V3Ast__gen_type_tests.h",
        ":V3Ast__gen_visitor_decls.h",
        ":V3Ast__gen_visitor_defns.h",
        ":V3Ast__gen_yystype.h",
        ":V3Const__gen.cpp",
        ":V3Dfg__gen_ast_to_dfg.h",
        ":V3Dfg__gen_auto_classes.h",
        ":V3Dfg__gen_dfg_to_ast.h",
        ":V3Dfg__gen_forward_class_decls.h",
        ":V3Dfg__gen_macros.h",
        ":V3Dfg__gen_type_enum.h",
        ":V3Dfg__gen_type_tests.h",
        ":V3Dfg__gen_visitor_decls.h",
        ":V3Dfg__gen_visitor_defns.h",
        ":V3ParseBison.h",
    ],
    hdrs = glob(["src/V3*.h"]) + [
        "src/config_build.h",
        "src/config_package.h",
        "src/config_rev.h",
    ],
    copts = [
        # TODO: We should probably set this later
        "-DDEFENV_SYSTEMC_INCLUDE=\\\"@invalid@\\\"",
        "-DDEFENV_SYSTEMC_LIBDIR=\\\"@invalid@\\\"",
        "-DDEFENV_VERILATOR_ROOT=\\\"@invalid@\\\"",
        # TODO: Remove these once upstream fixes these warnings
        "-Wno-unneeded-internal-declaration",
    ],
    defines = ["YYDEBUG"],
    strip_include_prefix = "src/",
    textual_hdrs = [
        # These are included directly by other C++ files
        # See https://github.com/bazelbuild/bazel/issues/680
        ":V3Lexer.yy.cpp",
        ":V3PreLex.yy.cpp",
        ":V3ParseBison.c",
    ],
    deps = [
        ":verilatedos",
        "@com_github_westes_flex//:FlexLexer",
    ],
)

cc_library(
    name = "verilator",
    srcs = [
        "include/gtkwave/fastlz.h",
        "include/gtkwave/fst_config.h",
        "include/gtkwave/fstapi.h",
        "include/gtkwave/lz4.h",
        "include/gtkwave/wavealloca.h",
        "include/verilated.cpp",
        "include/verilated_fst_c.cpp",
        "include/verilated_imp.h",
        "include/verilated_syms.h",
        "include/verilated_threads.cpp",
        "include/verilated_vcd_c.cpp",
    ],
    hdrs = [
        "include/verilated.h",
        "include/verilated_config.h",
        "include/verilated_dpi.h",
        "include/verilated_fst_c.h",
        "include/verilated_intrinsics.h",
        "include/verilated_threads.h",
        "include/verilated_sc.h",
        "include/verilated_sym_props.h",
        "include/verilated_trace.h",
        # Needed for verilated_vcd_c.cpp and verilated_fst_c.cpp
        "include/verilated_trace_imp.h",
        "include/verilated_vcd_c.h",
        "include/verilatedos.h",
        "include/verilated_types.h",
        "include/verilated_funcs.h",
    ],
    # TODO: Remove these once upstream fixes these warnings
    copts = [
        "-Wno-unused-const-variable",
        "-std=c++17",
        "-ffp-contract=off",
    ],
    includes = ["include"],
    linkopts = [
        "-lpthread",
        "-latomic",
    ],
    strip_include_prefix = "include/",
    textual_hdrs = [
        "include/gtkwave/fastlz.c",
        "include/gtkwave/fstapi.c",
        "include/gtkwave/lz4.c",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@net_zlib//:zlib",
    ],
)

# This alias is for supporting the legacy name but while allowing the
# `cc_library` target to be renamed to avoid a `liblibverilator.a`
# output name.
alias(
    name = "libverilator",
    actual = "verilator",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "svdpi",
    hdrs = [
        "include/vltstd/svdpi.h",
    ],
    strip_include_prefix = "include/vltstd",
    visibility = ["//visibility:public"],
)

cc_binary(
    name = "verilator_executable",
    srcs = ["src/Verilator.cpp"],
    copts = [
        "-std=c++17",
        "-ffp-contract=off",
    ],
    linkopts = [
        "-lpthread",
        "-latomic",
    ],
    visibility = ["//visibility:public"],
    deps = [":verilator_libV3"],
)
