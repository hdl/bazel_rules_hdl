# Copyright 2023 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# See https://github.com/bazelbuild/rules_python/blob/main/examples/pip_parse_vendored/BUILD.bazel
# for canonical vendoring setup.

load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")

package(
    default_applicable_licenses = ["//:package_license"],
    default_visibility = ["//visibility:private"],
)

compile_pip_requirements(
    name = "pip_requirements",
    timeout = "moderate",
    src = "pip_requirements.in",
)

genrule(
    name = "clean_requirements",
    srcs = ["@rules_hdl_pip_deps_to_vendor//:requirements.bzl"],
    outs = ["requirements.clean.bzl"],
    cmd = " | ".join([
        "cat $<",
        # Substitute for vendored dependencies directly.
        # We want to remove any references to the pip_parse repo used to generate
        # the dependencies,  otherwise the vendoring is not useful.
        "sed -e 's/rules_hdl_pip_deps_to_vendor/rules_hdl_pip_deps_vendored/g'",
        "sed -e 's|//\\([^:]*\\):pkg|_\\1//:pkg|g'",
        "sed -e 's|//\\([^:]*\\):whl|_\\1//:whl|g'",
        "sed -e 's|//\\([^:]*\\):data|_\\1//:data|g'",
        "sed -e 's|//{}:{}|_{}//:{}|g' >$@",
        # Replace the bazel 6.0.0 specific comment with something that bazel 5.4.0 would produce.
        # This enables this example to be run as a test under bazel 5.4.0.
        """sed -e 's#@rules_hdl//#@//#'""",
    ]) + " >$@",
)

write_file(
    name = "gen_update",
    out = "update.sh",
    content = [
        # This depends on bash, would need tweaks for Windows
        "#!/usr/bin/env bash",
        # Bazel gives us a way to access the source folder!
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -fv bazel-bin/dependency_support/requirements.clean.bzl dependency_support/requirements.bzl",
    ],
)

sh_binary(
    name = "vendor_requirements",
    srcs = ["update.sh"],
    data = [":clean_requirements"],
)

# Similarly ensures that the requirements.bzl file is updated
# based on the requirements.txt lockfile.
diff_test(
    name = "test_vendored",
    failure_message = "Please run:  bazel run //dependency_support:vendor_requirements",
    file1 = "requirements.bzl",
    file2 = ":clean_requirements",
)
