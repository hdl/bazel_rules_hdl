load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository")
load("//dependency_support/com_google_skywater_pdk:cell_libraries.bzl", "CELL_LIBRARIES")
load("//:repositories.bzl", "load_external")

def _hdl_ext_impl(module_ctx):
    root_direct_deps = []
    root_direct_dev_deps = []
    pdk_repo_names = {}

    for module in module_ctx.modules:
        for pdk in module.tags.pdk:
            for library_name in pdk.libraries:
                name = pdk.name + "_" + library_name

                if module.is_root:
                    if module_ctx.is_dev_dependency(pdk):
                        root_direct_dev_deps.append(name)
                    else:
                        root_direct_deps.append(name)

                if name not in pdk_repo_names:
                    pdk_repo_names[name] = ()
                    git_repository(
                        name = name,
                        commit = CELL_LIBRARIES[library_name]["commit"],
                        remote = "https://foss-eda-tools.googlesource.com/skywater-pdk/libs/%s.git" % library_name,
                        shallow_since = CELL_LIBRARIES[library_name]["shallow_since"],
                        build_file_content = """
load("@rules_hdl//dependency_support/com_google_skywater_pdk:declare_cell_library.bzl", "declare_cell_library")
declare_cell_library("{}", "{}")
""".format(name, library_name),
                        patches = CELL_LIBRARIES[library_name].get("patches", []),
                        # Strip one directory level. Useful for patches generated by git.
                        patch_args = [
                            "-p1",
                        ],
                    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = root_direct_deps,
        root_module_direct_dev_deps = root_direct_dev_deps,
    )

_PDK_TAG_ATTRS = {
    "name": attr.string(
        mandatory = True,
        doc = """A name for the repository.
The name must be unique within the set of names registered by this extension.
""",
    ),
    "libraries": attr.string_list(
        doc = "List of standard cell libraries.",
    ),
}

hdl_ext = module_extension(
    implementation = _hdl_ext_impl,
    tag_classes = {
        "pdk": tag_class(
            attrs = _PDK_TAG_ATTRS,
        ),
    },
)

def _hdl_tools_ext_impl(module_ctx):
    # Expose the internal repos so that third parties can access them.
    load_external()
    root_direct_deps = [
        "org_gnu_gnulib",
        "org_theopenroadproject",
        "org_theopenroadproject_asap7sc6t_26",
        "org_theopenroadproject_asap7sc7p5t_27",
        "org_theopenroadproject_asap7_pdk_r1p7",
        "org_theopenroadproject_asap7sc7p5t_28",
        "at_clifford_yosys",
        "edu_berkeley_abc",
        "tk_tcl",
        "org_sourceware_libffi",
        "com_google_skywater_pdk",
        "net_zlib",
        "verilator",
        "org_sourceware_bzip2",
        "org_gnu_readline",
        "org_gnu_gperf",
        "net_invisible_island_ncurses",
        "org_7zip",
        "org_swig",
        "com_github_quantamhd_lemon",
        "org_llvm_openmp",
        "bliss",
        "org_gnu_glpk",
        "org_pcre_ftp",
        "com_github_libbacktrace",
        "com_icarus_iverilog",
    ]
    return module_ctx.extension_metadata(
        root_module_direct_deps = root_direct_deps,
        root_module_direct_dev_deps = [],
    )


hdl_tools_ext = module_extension(
    implementation = _hdl_tools_ext_impl,
)
