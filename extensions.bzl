load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository")
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

load("//dependency_support/com_google_skywater_pdk:cell_libraries.bzl", "CELL_LIBRARIES")

_ASAP_CELL_LIBS = {
    "sc6t_26": {
        "remote": "https://github.com/The-OpenROAD-Project/asap7sc6t_26",
        "commit": "f572bf760c8bdc853cbafd0742790aba0780089c",
        "build": "//dependency_support/org_theopenroadproject_asap7sc6t_26:bundled.BUILD.bazel"
    },
    "sc7p5t_27": {
        "remote": "https://github.com/The-OpenROAD-Project/asap7sc7p5t_27",
        "commit": "900f55ed8bef025f39edcc8b8be5e04a2c55c15a",
        "build": "//dependency_support/org_theopenroadproject_asap7sc7p5t_27:bundled.BUILD.bazel"
    },
    "sc7p5t_28": {
        "remote": "https://github.com/The-OpenROAD-Project/asap7sc7p5t_28",
        "commit": "d88477438935a5a388bd6294f682dc405c93c5d2",
        "build": "//dependency_support/org_theopenroadproject_asap7sc7p5t_28:bundled.BUILD.bazel"
    },
}

def register_asap7_pdk(module_ctx, module, pdk, root_direct_deps, root_direct_dev_deps, pdk_repo_names):
    for library_name in pdk.libraries:
        name = pdk.name + "_" + library_name
        if module.is_root:
            if module_ctx.is_dev_dependency(pdk):
                root_direct_dev_deps.append(name)
            else:
                root_direct_deps.append(name)

        if name not in pdk_repo_names:
            pdk_repo_names[name] = ()
            lib_meta = _ASAP_CELL_LIBS[library_name]
            git_repository(
                name = name,
                commit = lib_meta["commit"],
                remote = lib_meta["remote"],
                build_file = Label(lib_meta["build"]),
            )

_GOOGLE_SKYWATER_PDK_BUILD_TEMPLATE = """
load("@rules_hdl//dependency_support/com_google_skywater_pdk:declare_cell_library.bzl", "declare_cell_library")
declare_cell_library("{}", "{}")
"""

def register_skywater_pdk(module_ctx, module, pdk, root_direct_deps, root_direct_dev_deps, pdk_repo_names):
    for library_name in pdk.libraries:
        name = pdk.name + "_" + library_name
        if module.is_root:
            if module_ctx.is_dev_dependency(pdk):
                root_direct_dev_deps.append(name)
            else:
                root_direct_deps.append(name)

        if name not in pdk_repo_names:
            pdk_repo_names[name] = ()
            git_repository(
                name = name,
                commit = CELL_LIBRARIES[library_name]["commit"],
                remote = "https://foss-eda-tools.googlesource.com/skywater-pdk/libs/%s.git" % library_name,
                shallow_since = CELL_LIBRARIES[library_name]["shallow_since"],
                build_file_content = _GOOGLE_SKYWATER_PDK_BUILD_TEMPLATE.format(name, library_name),
                patches = CELL_LIBRARIES[library_name].get("patches", []),
                # Strip one directory level. Useful for patches generated by git.
                patch_args = [
                    "-p1",
                ],
            )

def _hdl_ext_impl(module_ctx):
    root_direct_deps = []
    root_direct_dev_deps = []
    pdk_repo_names = {}

    http_archive(
        name = "com_google_skywater_pdk",
        build_file = "//dependency_support/com_google_skywater_pdk:bundled.BUILD.bazel",
        sha256 = "49e5b03c26131a03eb038697d396a6ebf14058d78196f5d95c2bbdb0bdc8f32e",
        strip_prefix = "skywater-pdk-3d7617a1acb92ea883539bcf22a632d6361a5de4",
        urls = [
            "https://github.com/google/skywater-pdk/archive/3d7617a1acb92ea883539bcf22a632d6361a5de4.tar.gz",  # 2020-12-04
        ],
    )
    root_direct_deps.append("com_google_skywater_pdk")
    for module in module_ctx.modules:
        for pdk in module.tags.skywater_pdk:
            register_skywater_pdk(
                module_ctx,
                module,
                pdk,
                root_direct_deps,
                root_direct_dev_deps,
                pdk_repo_names,
            )

    http_archive(
        name = "org_theopenroadproject_asap7_pdk",
        build_file = "@rules_hdl//dependency_support/org_theopenroadproject_asap7_pdk_r1p7:bundled.BUILD.bazel",
        sha256 = "b5847f93e55debb49d03ec581e22eb301109ff90c9ad19d35ae1223c70250391",
        strip_prefix = "asap7_pdk_r1p7-1ff7649bbf423207f6f70293dc1cf630cd477365",
        urls = [
            "https://github.com/The-OpenROAD-Project/asap7_pdk_r1p7/archive/1ff7649bbf423207f6f70293dc1cf630cd477365.tar.gz",
        ],
    )

    root_direct_deps.append("org_theopenroadproject_asap7_pdk")
    for module in module_ctx.modules:
        for pdk in module.tags.asap7_pdk:
            register_asap7_pdk(
                module_ctx,
                module,
                pdk,
                root_direct_deps,
                root_direct_dev_deps,
                pdk_repo_names,
            )

    return module_ctx.extension_metadata(
        root_module_direct_deps = root_direct_deps,
        root_module_direct_dev_deps = root_direct_dev_deps,
    )

_PDK_TAG_ATTRS = {
    "name": attr.string(
        mandatory = True,
        doc = """A name for the repository.
The name must be unique within the set of names registered by this extension.
""",
    ),
    "libraries": attr.string_list(
        doc = "List of standard cell libraries.",
    ),
}

hdl_ext = module_extension(
    implementation = _hdl_ext_impl,
    tag_classes = {
        "skywater_pdk": tag_class(
            attrs = _PDK_TAG_ATTRS,
        ),
        "asap7_pdk": tag_class(
            attrs = _PDK_TAG_ATTRS,
        ),
    },
)
